
# ecmaspeak-py/emu_eqn.grammar:
# A grammar for the content of <emu-eqn> elements.
#
# Copyright (C) 2018  J. Michael Dyck <jmdyck@ibiblio.org>

{START} :
    {EMU_EQN_DEF}{_eos_}

{EMU_EQN_DEF} :
    {CONSTANT_DEF}
    {OPERATION_DEF}

# --------------------------

{CONSTANT_DEF} :
    {CONSTANT_NAME} = {dec_int_lit}
    {CONSTANT_NAME} = {starred_int_lit}{h_sub_fancy_f}
    {CONSTANT_NAME} = {starred_int_lit}{h_sub_fancy_f} = {EXPR}

# --------------------------

{OPERATION_DEF} :
    {OP_NAME}({var}) = {EXPR}
    {_indent_}{nlai}{OP_NAME}({var}){_indent_}{RHSS}{_outdent_}{_outdent_}{nlai}

{RHSS} :
    {RHSS}{RHS}
    {RHS}

{RHS} :
    {nlai}= {EXPR} if {CONDITION}

{CONDITION} :
    {NUM_COMPARISON}
    {NUM_COMPARISON} and {NUM_COMPARISON}

{NUM_COMPARISON} :
    {NUM_COMPARAND} {NUM_COMPARATOR} {NUM_COMPARAND}
    {NUM_COMPARAND} {NUM_COMPARATOR} {NUM_COMPARAND} {NUM_COMPARATOR} {NUM_COMPARAND}

{NUM_COMPARAND} :
    {EXPR}

{NUM_COMPARATOR} :
    &le;
    &lt;
    &ne;
    =

# ------

{EXPR} :
    the largest integral Number {var} (closest to +&infin;) such that {CONDITION}
    {SUM}

{SUM} :
    {SUM} {SUM_OPERATOR} {TERM}
    {TERM}

{SUM_OPERATOR} :
    +
    -

{TERM} :
    {PRODUCT}

{PRODUCT} :
    {FACTOR}
    {FACTOR} {PRODUCT_OPERATOR} {FACTOR}

{PRODUCT_OPERATOR} :
    &times;
    /
    modulo

{FACTOR} :
    ({SUM})
    {CONSTANT_NAME}
    {NUM_LITERAL}
    {PREFIX_PAREN}
    {var}

{CONSTANT_NAME} :
    HoursPerDay
    MinutesPerHour
    SecondsPerMinute
    msPerDay
    msPerHour
    msPerMinute
    msPerSecond

{NUM_LITERAL} :
    {dec_int_lit}
    {starred_int_lit}{h_sub_fancy_f}

{PREFIX_PAREN} :
    {OPN_BEFORE_PAREN}({EXPR})

{OPN_BEFORE_PAREN} :
    {SIMPLE_OPERATION_NAME}

{SIMPLE_OPERATION_NAME} :
    floor
    {OP_NAME}
    {fancy_f}
    {fancy_r}

{OP_NAME} :
    DateFromTime
    Day
    DayFromYear
    DayWithinYear
    DaysInYear
    HourFromTime
    InLeapYear
    MinFromTime
    MonthFromTime
    SecFromTime
    TimeFromYear
    TimeWithinDay
    WeekDay
    YearFromTime
    msFromTime

# ------
