
# ecmaspeak-py/emu_eqn.grammar:
# A grammar for the content of <emu-eqn> elements.
#
# Copyright (C) 2018  J. Michael Dyck <jmdyck@ibiblio.org>

{START} :
    {EMU_EQN_DEF}{_eos_}

{EMU_EQN_DEF} :
    {CONSTANT_DEF}
    {OPERATION_DEF}

# --------------------------

{CONSTANT_DEF} :
    {CONSTANT_NAME} = {dec_int_lit}
    {CONSTANT_NAME} = {starred_int_lit}{h_sub_fancy_f}
    {CONSTANT_NAME} = {starred_int_lit}{h_sub_fancy_f} = {EXPR}

# --------------------------

{OPERATION_DEF} :
    {OP_NAME}({var}) = {EXPR}
    {OP_NAME}({var}){RHSS}{nlai}

{RHSS} :
    {RHSS}{RHS}
    {RHS}

{RHS} :
    {nlai}= {EXPR} if {CONDITION}

{CONDITION} :
    {COND}
    {COND} and {COND}

{COND} :
    {EXPR} &le; {EXPR}
    {EXPR} &le; {EXPR} &lt; {EXPR}
    {EXPR} &ne; {EXPR}
    {EXPR} = {EXPR}

# ------

{EXPR} :
    the largest integral Number {var} (closest to +&infin;) such that {CONDITION}
    {SUM}

{SUM} :
    {SUM} + {TERM}
    {SUM} - {TERM}
    {TERM}

{TERM} :
    {PRODUCT}

{PRODUCT} :
    {FACTOR}
    {FACTOR} &times; {FACTOR}
    {FACTOR} / {FACTOR}
    {FACTOR} modulo {FACTOR}

{FACTOR} :
    ({SUM})
    {CONSTANT_NAME}
    {PREFIX_PAREN}
    {dec_int_lit}
    {starred_int_lit}{h_sub_fancy_f}
    {var}

{PREFIX_PAREN} :
    {OPN_BEFORE_PAREN}({EXPR})

{OPN_BEFORE_PAREN} :
    floor
    {OP_NAME}
    {fancy_f}
    {fancy_r}

{CONSTANT_NAME} :
    HoursPerDay
    MinutesPerHour
    SecondsPerMinute
    msPerDay
    msPerHour
    msPerMinute
    msPerSecond

{OP_NAME} :
    DateFromTime
    Day
    DayFromYear
    DayWithinYear
    DaysInYear
    HourFromTime
    InLeapYear
    MinFromTime
    MonthFromTime
    SecFromTime
    TimeFromYear
    TimeWithinDay
    WeekDay
    YearFromTime
    msFromTime

# ------
